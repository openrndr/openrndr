import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.5.0"
        classpath "com.netflix.nebula:nebula-release-plugin:15.3.1"
    }
}

plugins {
    // remember to update all the versions here when upgrading kotlin version
    id 'org.jetbrains.kotlin.jvm' version '1.5.21' apply false
    id 'org.jetbrains.kotlin.multiplatform' version '1.5.21' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.21' apply false
}

apply plugin: 'org.jetbrains.dokka'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

/*
  Note: check out the latest Kotlin release docs to keep all in sync:

  https://kotlinlang.org/docs/releases.html#release-details

  Warning: coroutines and logging cannot be parametrized with project.ext
  see below!
*/
project.ext {
    kotlinApiVersion = '1.5'
    kotlinJvmTarget = '1.8'
    kotlinLanguageVersion = '1.5'
    kotlinVersion = '1.5.21'
    kotlinLoggingVersion = '2.0.6'
    kotlinxSerializationVersion = '1.2.1'
    kotlinxCoroutinesVersion = '1.5.0'
    lwjglVersion = '3.2.3'
    javacpp_version = '1.5.5'
    ffmpeg_version = "4.3.2-$javacpp_version"
    spekVersion = '2.0.15'
    kluentVersion = '1.65'
    jsoupVersion = '1.13.1'
    kotestVersion = '4.4.3'
    junitJupiterVersion = '5.7.1'
}

tasks.named("dokkaHtml") {
    moduleName.set("openrndr")
    dokkaSourceSets {

        named("main") { /* configure main source set */
            perPackageOption {
                matchingRegex.set(".*gl3.*")
                suppress.set(true)
            }
            perPackageOption {
                matchingRegex.set(".*internal*")
                suppress.set(true)
            }
            perPackageOption {
                matchingRegex.set(".*artifex*")
                suppress.set(true)
            }
            perPackageOption {
                matchingRegex.set("io\\.lacuna.*")
                suppress.set(true)
            }

        }
        skipDeprecated.set(true)
        skipEmptyPackages.set(true)
        remoteUrl.set(uri("https://github.com/openrndr/openrndr/master/src/main/kotlin").toURL())
    }


}

["openrndr-animatable",
 "openrndr-draw",
 "openrndr-dds",
 "openrndr-filter",
 "openrndr-core",
 "openrndr-math",
 "openrndr-color",
 "openrndr-event",
 "openrndr-shape",
 "openrndr-utils",
].each {

    project(":$it") {
        apply plugin: 'org.jetbrains.dokka'
    }
}


allprojects {
    group 'org.openrndr'
    repositories {
        mavenCentral()
    }
}

def multiplatformModules = [
        "openrndr-application",
        "openrndr-filter",
        "openrndr-event",
        "openrndr-math",
        "openrndr-color",
        "openrndr-utils",
        "openrndr-shape",
        "openrndr-binpack",
        "openrndr-animatable",
        "openrndr-draw",
        "openrndr-webgl",
        "openrndr-extensions",
        "openrndr-dds",
        "openrndr-openal",
        "openrndr-gl3",
        "openrndr-tessellation",
        "openrndr-svg",
        "openrndr-kartifex"
]


configure(allprojects.findAll { !multiplatformModules.contains(it.name) }) {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'

    dependencies {
        // Note: kotlin-logging and kotlinx-coroutines-core are loaded
        // too early and their versions cannot be parametrized
        implementation 'io.github.microutils:kotlin-logging:2.0.6'
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxCoroutinesVersion"

        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

        testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spekVersion"
        testImplementation "org.amshove.kluent:kluent:$kluentVersion"
        testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
        testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spekVersion"
        testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    }

    test {
        useJUnitPlatform {
            includeEngines 'spek2'
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    javadoc {
        options.addBooleanOption 'Xdoclint:none', true
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile).all {
        kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.InternalCoroutinesApi"]
    }
}

def doNotPublish = ["openrndr-demos"]

configure(allprojects.findAll { multiplatformModules.contains(it.name) && !doNotPublish.contains(it.name) }) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'


    def publicationsFromMainHost = ["jvm", "js"] + "kotlinMultiplatform"

    publishing {
//        publications {
//
//            matching { it.name in publicationsFromMainHost }.all { targetPublication ->
//                tasks.withType(AbstractPublishToMaven)
//                        .matching { it.publication == targetPublication }
//                        .configureEach { onlyIf { findProperty("isMainHost") == "true" } }
//            }
//        }

        repositories {
            maven {
                credentials {
                    username findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                    password findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
                }
                if (!isReleaseVersion) {
                    url "https://s01.oss.sonatype.org/content/repositories/snapshots"
                } else {
                    url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
                }
            }
        }
    }

    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
        sign publishing.publications
    }
}


configure(allprojects.findAll { !doNotPublish.contains(it.name) && (!multiplatformModules.contains(it.name)) }) {
    apply plugin: 'nebula.release'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                groupId = "org.openrndr"
                artifactId = "$project.name"
                description = "$project.name"
                pom {
                    name = "$project.name"
                    description = "$project.name"
                    url = 'https://openrndr.org'
                    developers {
                        developer {
                            id = 'edwinjakobs'
                            name = 'Edwin Jakobs'
                            email = 'edwin@openrndr.org'
                        }
                    }
                    license {
                        licenses {
                            license {
                                name = 'BSD-2-Clause'
                                url = 'https://github.com/openrndr/openrndr/blob/master/LICENSE'
                                distribution = 'repo'
                            }
                        }
                    }
                    scm {
                        connection = "scm:git:git@github.com:openrndr/openrndr.git"
                        developerConnection = "scm:git:ssh://github.com/openrndr/openrndr.git"
                        url = "https://github.com/openrndr/openrndr"
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                    password findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
                }
                if (!isReleaseVersion) {
                    url "https://s01.oss.sonatype.org/content/repositories/snapshots"
                } else {
                    url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
                }
            }
        }
    }
    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
        sign publishing.publications.mavenJava
    }
}

configure(allprojects.findAll { it.name != "openrndr-demos" }) {
    //apply plugin: 'maven-publish'
    apply plugin: 'nebula.release'
}
