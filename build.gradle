plugins {
    alias(libs.plugins.nebula.release)
    alias(libs.plugins.nmcp)
    id("org.openrndr.convention.dokka")
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'version-catalog'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

group = "org.openrndr"
nmcpAggregation {
    centralPortal {
        username.set(findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME"))
        password.set(findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD"))

        // publish manually from the portal
        publishingType = "USER_MANAGED"
    }
    publishAllProjectsProbablyBreakingProjectIsolation()
}

subprojects {
    // Equivalent Kotlin is: tasks.register<DependencyReportTask>("dependenciesAll") { ...
    tasks.register("dependenciesAll", DependencyReportTask) {
        group = HelpTasksPlugin.HELP_GROUP
        description = "Displays all dependencies, including subprojects."
    }
}

catalog {
    // declare the aliases, bundles and versions in this block
    versionCatalog {
        version('openrndr', "${version}")
        library('animatable', 'org.openrndr', 'openrndr-animatable').versionRef('openrndr')
        library('application', 'org.openrndr', 'openrndr-application').versionRef('openrndr')
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.versionCatalog
        }
    }

}

dependencies {
    dokka(project(":openrndr-animatable:"))
    dokka(project(":openrndr-application:"))
    dokka(project(":openrndr-binpack:"))
    dokka(project(":openrndr-color:"))
    dokka(project(":openrndr-dds:"))
    dokka(project(":openrndr-jvm:openrndr-dialogs:"))
    dokka(project(":openrndr-draw:"))
    dokka(project(":openrndr-event:"))
    dokka(project(":openrndr-extensions:"))
    dokka(project(":openrndr-jvm:openrndr-ffmpeg:"))
    dokka(project(":openrndr-filter:"))
    dokka(project(":openrndr-gl-common:"))
    dokka(project(":openrndr-jvm:openrndr-gl3:"))
    dokka(project(":openrndr-kartifex:"))
    dokka(project(":openrndr-ktessellation:"))
    dokka(project(":openrndr-math:"))
    dokka(project(":openrndr-nullgl:"))
    dokka(project(":openrndr-jvm:openrndr-openal:"))
    dokka(project(":openrndr-platform:"))
    dokka(project(":openrndr-shape:"))
    dokka(project(":openrndr-utils:"))
    dokka(project(":openrndr-js:openrndr-webgl:"))
}

gradle.buildFinished {
    println("\n")
    println("openrndr = \"${version}\"")
}
